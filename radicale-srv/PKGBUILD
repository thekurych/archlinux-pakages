# Maintainer: unknown

pkgname=radicale-srv
_pkgname=radicale
_source_name=Radicale
pkgver=1.1.1
pkgrel=2
pkgdesc='Simple calendar (CalDAV) and contact (CardDAV) server'
arch=('any')
url='http://www.radicale.org/'
license=('GPL3')
depends=('python>=3.2')
makedepends=('python-setuptools')
backup=('etc/radicale/config' 'etc/radicale/rights')
conflicts=('radicale')
install='radicale.install'
source=(
	Radicale-${pkgver}.tar.gz::"https://github.com/Kozea/Radicale/archive/${pkgver}.tar.gz"
	"${pkgname}-${pkgver}.patch"
	'radicale.service'
	'radicale.install'
	'radicale.nginx-example.conf'
	'radicale.uwsgi-example.ini'
	'radicale.rights'
)
optdepends=(
	'python-passlib: md5 and bcrypt support for htpasswd access'
	'python-requests: HTTP authentication support'
	'python-dulwich: Git support'
#	'python-pam: PAM authentication support'
#	'python-ldap: LDAP authentication support'
)

prepare() {
  cd "${_source_name}-${pkgver}"

  patch -p1 < "${srcdir}/${pkgname}-${pkgver}.patch"
}

package() {
  pushd ${_source_name}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m644 -D 'config' "${pkgdir}/etc/${_pkgname}/config"

  install -m644 -D 'radicale.fcgi' "${pkgdir}/usr/share/${_pkgname}/radicale.fcgi"
  install -m644 -D 'radicale.wsgi' "${pkgdir}/usr/share/${_pkgname}/radicale.wsgi"
  popd

  install -m644 -D 'radicale.service' "${pkgdir}/usr/lib/systemd/system/radicale.service"
  install -m644 -D 'radicale.nginx-example.conf' "${pkgdir}/usr/share/${_pkgname}/radicale.nginx-example.conf"
  install -m644 -D 'radicale.uwsgi-example.ini' "${pkgdir}/usr/share/${_pkgname}/radicale.uwsgi-example.ini"
  install -m644 -D 'radicale.rights' "${pkgdir}/etc/${_pkgname}/rights"
}

sha256sums=('f978e6bdfab329a8d2d643584f02c4d2788a44b360ed4c326dea9e2735df81f6'
            '9231c44c28baed0b2a74d9403b0091cf24987df687e9de35041387ec12d15b8d'
            '8de8b5198f7d07d463c351fbe89aed4d0b392f97c52cf8feb04218f90918f7ac'
            '8c7913922ca72c757c849be0e609e2dc2aebd07cba116a8b8be67b91adffb00f'
            '3852891d79afa238f60da9d686f4aaf3a52fe4c05d3e27c635e90504b88e294a'
            '93ea91cac7386bbdde97fdad46bed068fa43bca4a60dcc2af3c2f04cd937980a'
            '4f25fe806f27ca4e3b12a2f7d7c3e867b2ea6683285e0f4a289e3a1548d85ebf')

