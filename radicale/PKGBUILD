# Maintainer: unknown

pkgname=radicale
_pkgname=Radicale
pkgver=1.1.1
pkgrel=6
pkgdesc='Simple calendar (CalDAV) and contact (CardDAV) server'
arch=('any')
url='http://www.radicale.org/'
license=('GPL3')
depends=('python>=3.2')
makedepends=('python-setuptools')
backup=('etc/radicale/config')
install='radicale.install'
source=(
	Radicale-${pkgver}.tar.gz::"https://github.com/Kozea/Radicale/archive/${pkgver}.tar.gz"
	"radicale-${pkgver}.patch"
	'radicale.service'
	'radicale.install'
)
optdepends=(
	'python-passlib: md5 and bcrypt support for htpasswd access'
	'python-requests: HTTP authentication support'
	'python-dulwich: Git support'
#	'python-pam: PAM authentication support'
#	'python-ldap: LDAP authentication support'
)

prepare() {
  cd "${_pkgname}-${pkgver}"

  patch -p1 < "${srcdir}/${pkgname}-${pkgver}.patch"
}

package() {
  pushd ${_pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  install -m644 -D 'config' "${pkgdir}/etc/radicale/config"

  install -m644 -D 'radicale.fcgi' "${pkgdir}/usr/share/${pkgname}/radicale.fcgi"
  install -m644 -D 'radicale.wsgi' "${pkgdir}/usr/share/${pkgname}/radicale.wsgi"
  popd

  install -m644 -D 'radicale.service' "${pkgdir}/usr/lib/systemd/system/radicale.service"
}

sha256sums=('f978e6bdfab329a8d2d643584f02c4d2788a44b360ed4c326dea9e2735df81f6'
            'e9089df51c5b77741cb153cd66eb4d78ca25ff4c2bd20d638675846aa0574df3'
            '8de8b5198f7d07d463c351fbe89aed4d0b392f97c52cf8feb04218f90918f7ac'
            '5d31c4eb292019eb5cfb9d11e256d15a330e968d3c9bc9a077f9c1fa4ecf0089')
